plugins {
	id 'java'
	id 'antlr'
	id 'maven-publish'
	id 'com.jfrog.bintray' version '1.8.4'
	id 'de.clashsoft.simple-publish' version '0.2.0'
	id 'de.clashsoft.gentreesrc-gradle' version '0.3.0'
}

group 'de.clashsoft'
version '0.7.0'
description 'A tool that generates Tree and Visitor classes from an AST model description file.'

publishInfo {
	websiteUrl = 'https://github.com/Clashsoft/gentreesrc'
	issueTrackerUrl = 'https://github.com/Clashsoft/gentreesrc/issues'
	vcsUrl = 'https://github.com/Clashsoft/gentreesrc'
	githubRepo = 'Clashsoft/gentreesrc'
	labels = [ 'source', 'generator', 'tree', 'ast' ]

	license {
		shortName 'BSD 3-Clause'
		longName 'BSD 3-Clause "New" or "Revised" License'
		url 'https://opensource.org/licenses/BSD-3-Clause'
	}

	developer {
		id 'Clashsoft'
		name 'Adrian Kunz'
		email 'clashsoft@hotmail.com'
	}
}

sourceCompatibility = 1.8

repositories {
	mavenCentral()
	jcenter()
}

configurations {
	compile {
		extendsFrom = extendsFrom.findAll { it != configurations.antlr }
	}
}

dependencies {
	gentreesrc group: 'de.clashsoft', name: 'gentreesrc', version: '0.5.0'

	testCompile group: 'junit', name: 'junit', version: '4.12'

	// https://mvnrepository.com/artifact/org.antlr/antlr4
	antlr group: 'org.antlr', name: 'antlr4', version: '4.7.2'

	// https://mvnrepository.com/artifact/org.antlr/antlr4-runtime
	compile group: 'org.antlr', name: 'antlr4-runtime', version: '4.7.2'

	// https://mvnrepository.com/artifact/org.antlr/ST4
	compile group: 'org.antlr', name: 'ST4', version: '4.1'

	// https://mvnrepository.com/artifact/commons-cli/commons-cli
	compile group: 'commons-cli', name: 'commons-cli', version: '1.4'
}

// https://github.com/gradle/gradle/issues/7706#issuecomment-470999724
def oldGroup = project.group
try {
	project.group = 'dummy_group_for_bootstrap'
	configurations.gentreesrc.files
}
finally {
	project.group = oldGroup
}

// override test task classpath with newest version (this one)
gentreesrcTestJava.classpath = sourceSets.main.runtimeClasspath

// --------------- ANTLR ---------------

def generatedANTLRDir = "$buildDir/generated-src/antlr/main/"

sourceSets.main.java.srcDir(generatedANTLRDir)

// https://stackoverflow.com/questions/40995727/gradle-cant-find-antlr-token-file
generateGrammarSource {
	def packageName = 'de.clashsoft.gentreesrc.antlr'
	arguments += [ '-package', packageName ]
	outputDirectory = file("$generatedANTLRDir/${ packageName.replace('.', '/') }/")
}
